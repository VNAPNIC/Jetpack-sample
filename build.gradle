
buildscript {
    // Define versions in a single place
    ext {
        // Sdk and tools
        compileSdkVersion = 28
        minSdkVersion = 19
        targetSdkVersion = 28

        // App dependencies
        constraintLayoutVersion = '1.1.2'
        espressoVersion = '3.0.1'
        glideVersion = '4.4.0'
        gradleVersion = '3.2.0-beta04'
        gsonVersion = '2.8.2'
        junitVersion = '4.12'
        lifecycleVersion = '1.1.1'
        navigationVersion = '1.0.0-alpha03'
        kotlinVersion = '1.2.51'
        ktlintVersion = '0.24.0'
        ktxVersion = '0.3'
        roomVersion = '1.1.1'
        runnerVersion = '1.0.1'
        supportLibraryVersion = '28.0.0-alpha3'
        workVersion = '1.0.0-alpha04'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.13.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        ktlint(ktlintVersion)
    }
}
